# Maintainer: Michael Picht <michael dot picht at ussenterprise dot de>

pkgname=gool-git
pkgver=0.9.5
pkgrel=1
pkgdesc='Go - Online TV Recorder on Linux'
arch=('x86_64' 'i686')
url="http://github.com/mipimipi/$pkgname/"
license=('GPL3')
depends=('mkvtoolnix-cli')
makedepends=('go' 'git')
source=("git://github.com/mipimipi/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  export GOPATH=$(pwd)
  mkdir src
  ln -s -r vendor/github.com src/github.com
  ln -s -r vendor/golang.org src/golang.org
}

build() {
  cd "$pkgname"
  export GOPATH=$(pwd)
  make
}

package() {
  cd "$pkgname"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 resources/gool.desktop "$pkgdir/usr/share/applications/gool.desktop"
  install -Dm644 resources/otrkey_mime.xml "$pkgdir/usr/share/mime/packages/otrkey_mime.xml"
}